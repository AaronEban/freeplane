dependencies {
    compile project(':freeplane')
    compile group: 'org.scilab.forge', name: 'jlatexmath', version:'1.0.2'
}

version = '1.3.1'

task pluginJar(type: Jar) {
     baseName = 'plugin'
     dependsOn classes

     from (sourceSets.main.output) {
         include("**")
     }
     includeEmptyDirs = false

     // use a non-OSGi manifest (this jar is referenced from an OSGi bundle)!
     project.manifest { }
}

jar {
    manifest {
        name = pluginid
        symbolicName = pluginid

        ext.deps = configurations.runtime.files.collect { "lib/${it.name}" }
        ext.deps.add(0, "lib/plugin-" + project.version + '.jar')

        attributes 'Class-Path': ext.deps.join(', ')
        instruction 'Bundle-Vendor', 'Freeplane Project'
        instruction 'Bundle-Activator', pluginid + ".Activator"
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.6'
        instruction 'Import-Package', 'org.freeplane.plugin.script, org.osgi.framework;version="1.3.0", org.freeplane.core.*, org.freeplane.features.*, org.freeplane.main.*, org.freeplane.n3.*, org.freeplane.view.*'
        instruction 'Export-Package', ''

        // Require-Bundle is ignored by bnd => replaced by Import-Package!
        //instruction 'Require-Bundle', 'org.freeplane.core;bundle-version="1.0.1", org.freeplane.plugin.script;bundle-version="1.0.1"'
    }

    // must execute after pluginJar so that the OSGi manifest can still be
    // picked up from build/tmp/jar!
    dependsOn pluginJar
}
