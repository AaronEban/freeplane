plugins {
	id "net.ltgt.apt" version "0.13"
  }
  
apply plugin: "net.ltgt.apt-eclipse"

repositories {
	maven { url "${project.gradle.gradleUserHomeDir}/local-artifacts" }
}

  
dependencies {
    compileOnly project(':freeplane')
    testCompile project(':freeplane')
	compileOnly "org.immutables:value:2.2.10:annotations"
	apt         "org.immutables:value:2.2.10"
	compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
	compile 'com.fasterxml.jackson.core:jackson-core:2.9.0'
	compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.0'
	compile 'org.freeplane.collaboration:freeplane-events:0.0.1-SNAPSHOT'

	compile 'javax.websocket:javax.websocket-api:1.0'
	// websocket (reference) implementation:
	compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client-jdk:1.13.1'
	//compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.11.1'
}

eclipse {
  jdt {
    apt {
      genSrcDir = file('apt_generated')
    }
  }
}

jar {
    manifest {
        name = pluginid
        symbolicName = pluginid

        configurations.runtime.transitive = false
        def deps = filterAndExternalizePluginDependencies(configurations.runtime.files)
        deps.add(0, "lib/plugin-" + project.version + '.jar')

        attributes 'Class-Path': deps.join(', ')
        attributes 'Bundle-ClassPath': '., ' + deps.join(', ')

        instruction 'Bundle-Vendor', 'Freeplane Project'
        instruction 'Bundle-Activator', 'org.freeplane.plugin.collaboration.client.Activator'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.8'

        def imports = """\
                      org.osgi.framework;version="1.3.0",
                      org.freeplane.core.*,
                      org.freeplane.features.*,
                      org.freeplane.main.*,
                      org.freeplane.n3.*,
                      org.freeplane.view.*
                      """
        instruction 'Import-Package', imports.replaceAll("\\s+", "")
        instruction 'Export-Package', ''

        // Require-Bundle is ignored by bnd => replaced by Import-Package!
        //instruction 'Require-Bundle', 'org.freeplane.core;bundle-version="1.0.1"'
    }
}
