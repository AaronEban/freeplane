subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

configure(subprojects.findAll {it.name =~ /plugin/ || it.name =~ /^freeplane$/ || it.name =~ /^freeplane_framework$/}){
    repositories {
        maven {
            url 'http://www.knopflerfish.org/maven2/'
        }
    }

    dependencies {
        compile group: 'org.knopflerfish', name:'frameworkbundle', version: '4.1.10'
    }
}

// /*|| it.name =~ /^freeplane$/*/
configure(subprojects.findAll {it.name =~ /plugin/}) {
    apply plugin: 'osgi'

    ext.pluginid = project.name.replace("freeplane_plugin_", "org.freeplane.plugin.")

    task copy_plugin_jars(type: Copy) {
         from ('build/libs') {
              include("*.jar")
         }
         from ('lib') {
              include("*.jar")
         }
         into('../DIST/plugins/' + pluginid + '/lib/')
    }

    task copy_plugin_manifest(type: Copy) {
         from ('META-INF') {
              include("*.MF")
         }
         into('../DIST/plugins/' + pluginid + '/META-INF/')
    }


    copy_plugin_jars.dependsOn(jar)
    build.dependsOn(copy_plugin_jars) // TODO: make 'check' depend on it instead of 'build'?
    build.dependsOn(copy_plugin_manifest)
}

apply plugin: 'base' // necessary to get the global 'clean' task

// remove DIST/**
task cleanDIST(type: Delete) {
    delete './DIST'
}
clean.dependsOn(cleanDIST)